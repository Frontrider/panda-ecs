group = 'org.pandaframework'
version = "0.1.0-SNAPSHOT"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.0.RC2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
    }
}

apply plugin: "kotlin"
apply plugin: "idea"

repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/jetbrains/spek"
    }
    maven {
        url "https://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
}

apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
    platformVersion '1.0.0-M3'
    filters {
        engines {
            include 'spek'
        }
    }
}

apply plugin: "io.spring.dependency-management"

dependencyManagement {
    dependencies {
        dependency "com.natpryce:hamkrest:1.1.0.0"

        dependency "com.winterbe:expekt:0.2.0"

        dependencySet(group: 'org.junit.platform', version: '1.0.0-M3') {
            entry 'junit-platform-engine'
            entry 'junit-platform-runner'
            entry 'junit-platform-launcher'
        }

        dependencySet(group: 'org.jetbrains.spek', version: '1.1.0-beta3') {
            entry ('spek-api') {
                exclude group: 'org.jetbrains.kotlin'

            }

            entry ('spek-subject-extension') {
                exclude group: 'org.jetbrains.kotlin'

            }

            entry ('spek-junit-platform-engine') {
                exclude group: 'org.jetbrains.kotlin'
            }
        }

        dependency "com.nhaarman:mockito-kotlin:1.2.0"

        dependencySet(group: 'org.jetbrains.kotlin', version: ext.kotlinVersion) {
            entry 'kotlin-stdlib'
            entry 'kotlin-reflect'
        }
    }
}

configurations {
    integrationTestCompile {
        extendsFrom testCompile
    }

    integrationTestRuntime {
        extendsFrom testRuntime
    }
}

sourceSets {
    integrationTest {
        kotlin {
            srcDirs += 'src/integrationTest/kotlin'
        }
        compileClasspath += sourceSets.main.output
    }
}

idea {
    module {
        sourceDirs -= file('src/integrationTest/kotlin')
        testSourceDirs += file('src/integrationTest/kotlin')
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    testCompile 'com.natpryce:hamkrest'
    testCompile 'org.jetbrains.spek:spek-api'
    testCompile 'com.winterbe:expekt'
    testCompile 'com.nhaarman:mockito-kotlin'
    testCompile 'org.jetbrains.spek:spek-subject-extension'

    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine'
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'panda-ecs'

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
